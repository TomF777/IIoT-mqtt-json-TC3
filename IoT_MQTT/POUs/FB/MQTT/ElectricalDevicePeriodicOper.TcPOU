<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.15">
  <POU Name="ElectricalDevicePeriodicOper" Id="{4e4e7ef3-5fbd-42e2-843d-e5281a590f85}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK ElectricalDevicePeriodicOper
VAR_INPUT
	LineName					: STRING(40);
	MachineName 				: STRING(40);
	DeviceName					: STRING(40);
	TimeStamp					: ULINT;
	DeviceElectricalData 		: ElectrDataEthCAT;
	DeviceRunning				: BOOL;
	SynchPulse					: BOOL;
	Enable						: BOOL;
END_VAR
VAR_OUTPUT
	// Topic
	MqttTopic 					: STRING(255);
	
	// JSON payload
	MqttJsonPayload 			: STRING(4000);
	
	// Publish Command 
	PublishMQTT 				: BOOL;
END_VAR
VAR
	SetParameter 				: BOOL := TRUE;
		
	// state machine
	nState						: INT;		
		
	// JSON document
  	fbJson			: FB_JsonSaxWriter;
	fbJsonDataType	: FB_JsonReadWriteDataType;
	sJsonDocument	: STRING(4000);
	stValues		: ElectrDevicePeriodicOper_DataModel;	
	
	// temporary mqtt topic
	TopicString : STRING;
	
	// device state detection
	rtrigDeviceRunning: R_TRIG;
	ftrigDeviceRunning: F_TRIG;

	// device state
	DeviceState			: BOOL;

	// safety watchdog
	tonWatchdog : TON;	
	
	StoppingCntr 	: INT;
	StoppedCntr 	: INT;
	
	 // Drive States
     RunningState	: BOOL := FALSE;
     StoppingState	: BOOL := FALSE;
     StoppedState	: BOOL := FALSE;
	 DriveStates	: DriveState_DataModel; 
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*
This FB evaluates electrical data from EL3443 when electrical device is in 'Running'
state and sends it by mqtt.
*)

IF Enable THEN
		DeviceState := DeviceRunning;
		
		// synchronisation pulse from FB input
		stValues.SynchPulse := SynchPulse;
		
		// edge detection for drive state evaluation
		rtrigDeviceRunning(CLK:=DeviceState);	
		ftrigDeviceRunning(CLK:=DeviceState);	
		
		IF SetParameter THEN
			SetParameter := FALSE;
			PublishMQTT := FALSE;
			// create mqtt topic
			TopicString := CONCAT(LineName, MachineName);
			TopicString := INSERT(TopicString,'/',LEN(TopicString)-LEN(MachineName));
			TopicString := CONCAT(TopicString, DeviceName);
			TopicString := INSERT(TopicString,'/',LEN(TopicString)-LEN(DeviceName));
			MqttTopic := TopicString;
			
			// constant entry in json payload
			stValues.LineName := LineName;
			stValues.MachineName := MachineName;
			stValues.DeviceName := DeviceName;
		END_IF
		
		
		// evaluate device state
		CASE nState OF
		0:
		// IDLE
			StoppingCntr 		:= 0;
			StoppedCntr 		:= 0;
			RunningState 		:= FALSE;
			StoppingState	 	:= FALSE;
			StoppedState 		:= FALSE;
			PublishMQTT  		:= FALSE;
			stValues.DeviceState := DriveStates.StoppedState;
			
			IF rtrigDeviceRunning.Q THEN
				// sequence watchdog
				tonWatchdog.IN := TRUE; 
				nState := 10;
			END_IF
			
		10:
		// RUNNING
			RunningState := TRUE;
			stValues.DeviceState := DriveStates.RunningState;
			
			PublishMQTT := TRUE;
			
			IF ftrigDeviceRunning.Q THEN
				RunningState := FALSE;
				nState := 20;
			END_IF
		
		20:
		// STOPPING
			StoppingState 			:= TRUE;
			stValues.DeviceState 	:= DriveStates.StoppingState;
			tonWatchdog.IN := FALSE;
			
			IF StoppingState THEN StoppingCntr := StoppingCntr + 1; END_IF
			
			IF StoppingCntr = 3 THEN
				StoppingState := FALSE;
				nState := 30;
			END_IF
		
		30:
		// STOPPED
			StoppedState 			:= TRUE;
			stValues.DeviceState 	:= DriveStates.StoppedState;
		
			IF StoppedState THEN StoppedCntr := StoppedCntr + 1; END_IF
			
			IF StoppedCntr = 2 THEN
				StoppedState := FALSE;
				PublishMQTT := FALSE;
				nState := 0;
			END_IF
			
		END_CASE
		
		
		IF PublishMQTT THEN
			//write electrical data from FB input to JSON payload
			stValues.ElectricalData := DeviceElectricalData;
			stValues.TimeStamp := TimeStamp;
		
			//create JSON message
			fbJson.ResetDocument();
			fbJsonDataType.AddJsonValueFromSymbol(fbJson, 'ElectrDevicePeriodicOper_DataModel', SIZEOF(stValues), ADR(stValues));
			fbJson.CopyDocument(sJsonDocument, SIZEOF(sJsonDocument));
			MqttJsonPayload := sJsonDocument;
		END_IF
ELSE
		PublishMQTT := FALSE;
		tonWatchdog.IN := FALSE;
		nState := 0;
END_IF

IF tonWatchdog.ET > T#10S THEN
	nState := 0;
END_IF
tonWatchdog(PT:=T#10S);]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>